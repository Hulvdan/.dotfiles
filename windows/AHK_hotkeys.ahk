;-----------------------------------------------------------------------------------------
; Это моя "надстройка" на Windows для удобного набора спецсимволов слепой печатью,
; а также парочки других удобных кнопок.
;
; Я переназначаю `Caps Lock` на `F24` (с помощью PowerToys)
; и проставляю тут набор сочетаний клавиш `F24 + {что-то ещё}`.
;
; NOTE: Раньше я переназначал `Caps Lock` на `Ctrl`, чтобы vim / tmux
;       было удобнее использовать. Но в итоге пришёл к этому варианту
;       и снова вернулся к использованию vim с `Ctrl`.
;
; Добавленные сочетания клавиш:
;
;   (основной ряд, левая рука)
;     F24 + S -> [
;     F24 + D -> {
;     F24 + F -> (
;     F24 + G -> &
;
;   (основной ряд, правая рука)
;     F24 + H -> =
;     F24 + J -> )
;     F24 + K -> }
;     F24 + L -> ]
;
;   (верхний ряд, левая рука)
;     F24 + W -> +
;     F24 + E -> -
;
;   (верхний ряд, правая рука)
;     F24 + U -> \
;     F24 + I -> *
;     F24 + O -> /
;
;   (нижний ряд, левая рука)
;     F24 + V -> ^
;
;   (нижний ряд, правая рука)
;     F24 + M -> $
;     F24 + , -> «
;     F24 + . -> »
;
;   NOTE: Небольшая особенность в отзеркаливании некоторых клавиш:
;         - Безымянные набирают   []
;         - Средние набирают      {}
;         - Указательные набирают (), ^$ (перейти в начало, в конец в vim)
;
; Изменённые клавиши для быстрого набора snake_case / CONSTANT_CASE.
; Сделал так, чтобы работало только на английском,
; чтобы не переназначать русскую букву `х`.
;
;     Клавиша `[` всегда набирает `_`
;
;     Клавиша `{` (т.е. `shift + [`) всегда набирает `_`.
;
; Как использовать (на момент 2024-07-27):
;   1. Переназначить `Caps Lock` на `F24`.
;      - Установить PowerToys.
;        https://learn.microsoft.com/en-us/windows/powertoys/install
;      - Запустить.
;      - Перейти в `Keyboard Manager`.
;      - `Remap a key`.
;      - `Caps lock` на `F24`.
;   2. Установить AutoHotkey. https://www.autohotkey.com/
;   3. Убедиться, что сейчас активна английская раскладка.
;   4. Запустить этот скрипт от имени администратора.
;      - Для этого нужно создать shortcut (ПКМ по файлу скрипта -> Create Shortcut).
;      - В его настройках, в advanced поставить галку `Run As Administrator`.
;      - Запустить этот shortcut.
;   5. Готово.
;
; Как я научился использовать новые сочетания клавиш:
;   1. Отрубил работу спецсимволов в местах по-умолчанию
;      (ниже можно специально для этого раскомментить кусок кода).
;
;   2. Кодил.
;
;   3. Иногда устраивал себе тренировки:
;      - Заходил на https://monkeytype.com/
;      - Выбирал режим `custom`.
;      - Нажимал `change`.
;      - И указывал группы подряд идущих символов. Например:
;
;            +]^\ =$$\^ $-[- [}\]$ /&{}- -)[\ /(\/= \&}+ *+[* +-}$\ $}] **\ []) $$(]*
;            +^/ -)/{ =/) ^*{ ]\&&- {$^) ]/( ]\^ -$/)] ))]- {}}\( [/][} =}-/ (/&{$ -=-}
;            =+)[ ++}+ (([]- &)- ](/) ^*^ $*}+ {^*}) \^)\] +^([^ ^(^-^ $&\* ^=(^( [=]=
;            /\= &\)/\ +)- *-)& **[* ^\/} ])\$ *\( ${&$/ &{) \-/&+ (]- [)]/ &&+&^ ^/={+
;            )\\( +{[( +}}-[ $ ][*& =(/ (=={+ /+* ]$(== $*^ -[-+) $]* +{\( [{)( \=^+$
;            *)[ &(]- */\{ +(} *-^/& )])\[ ))) {=+/$ $]^} \$}}[ *^*& *[$$- &{$/ (^]=\
;            {}= ^=]{ })\^) )({/ &]})& }(( } [&-{ }/\$ ]++-$ ={}) /=*& ^=( $*^ -}/+[
;
;        NOTE: Сгенерировано с помощью python:
;
;              ```
;              from random import randint, choices
;
;              symbols = "[{()}]&=+-*/\^$"
;              groups = []
;              for i in range(100):
;                  k = randint(3, 5)
;                  groups.append("".join(choices(symbols, k=k)))
;
;              print(" ".join(groups))
;              ```
;-----------------------------------------------------------------------------------------
#SingleInstance force
#NoEnv

SendMode Input

; NOTE: Функцию перезагрузки скрипта раскомменчиваю, когда активно работаю над ним.
; f9::fnReload()

; `Right Ctrl + Right Shift` для кнопки вызова окна контекста,
; т.к. у меня на клавиатуре её нет. Использую редко.
RShift & RControl::Send {AppsKey}
RControl & RShift::Send {AppsKey}

; `Ctrl + Alt + Shift + F12` убирает / возвращает title bar окон.
; Я ценю вертикальное пространство и не готов его отдавать под пустоту,
; занимаемую title bar-ом.
^!+F12::fnToggleTitleBar()

;-----------------------------------------------------------------------------------------
; Набор спецсимволов в удобных местах клавиатуры при печати в слепую.
; У AHK немного конченное экранирование символов, поэтому выглядит неконсистентно.
;-----------------------------------------------------------------------------------------
F24 & S::Send, {TEXT}[
F24 & D::Send, {TEXT}{
F24 & F::Send, {TEXT}(
F24 & G::&
F24 & H::=
F24 & J::Send, {TEXT})
F24 & K::Send, {TEXT}}
F24 & L::Send, {TEXT}]

F24 & W::SendRaw, +
F24 & E::Send, -
F24 & U::Send, \
F24 & I::Send, *
F24 & O::Send, /

F24 & V::SendRaw, % "^"
F24 & M::SendRaw, % "$"
F24 & ,::SendRaw, % "«"
F24 & .::SendRaw, % "»"

;-----------------------------------------------------------------------------------------
; Эксперименитровал с набором цифр на ряду ниже домашнего.
; Не использую, т.к. я слишком слаб :/
;-----------------------------------------------------------------------------------------
; F24 & Z::Send, 1
; F24 & X::Send, 2
; F24 & C::Send, 3
; F24 & V::Send, 4
; F24 & B::Send, 5
; F24 & N::Send, 6
; F24 & M::Send, 7
; F24 & <::Send, 8
; F24 & >::Send, 9
; F24 & /::Send, 0

;-----------------------------------------------------------------------------------------
; Раскомментить, чтобы отключить работу спецсимволов по-умолчанию.
; Помогает привыкнуть к новому расположению спецсимволов.
;-----------------------------------------------------------------------------------------
; *(::fnDoNothing()
; *)::fnDoNothing()
; *{::fnDoNothing()
; *}::fnDoNothing()
; *[::fnDoNothing()
; *]::fnDoNothing()
; *-::fnDoNothing()
; *+::fnDoNothing()
; *=::fnDoNothing()
; *^::fnDoNothing()
; *$::fnDoNothing()

;-----------------------------------------------------------------------------------------
; Установление breakpoint-а в Visual Studio с помощью дополнительных кнопок на мышке.
;-----------------------------------------------------------------------------------------
#IfWinActive, ahk_exe devenv.exe
XButton1::SendEvent, {F9}
XButton2::SendEvent, {F9}

;-----------------------------------------------------------------------------------------
; Замена `[` и `{` на `_` при использовании английского языка.
;-----------------------------------------------------------------------------------------
#if fnIsEnglishLayoutActive()
{::Send, {TEXT}_
[::Send, {TEXT}_

fnIsEnglishLayoutActive() {
    return DllCall("GetKeyboardLayout", "UInt"
        , DllCall("GetWindowThreadProcessId", "UInt", WinExist("A"), "UInt", 0)) == 67699721
}

fnReload() {
    SoundPlay, *-1
    reload
}

fnDoNothing() {
}

fnToggleTitleBar() {
    WinSet, Style, ^0xC00000, A
}
